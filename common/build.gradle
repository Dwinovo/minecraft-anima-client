plugins {
    id 'multiloader-common'
    id 'net.neoforged.moddev'
}

neoForge {
    neoFormVersion = neo_form_version
    // Automatically enable AccessTransformers if the file exists
    def at = file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from(at.absolutePath)
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
}

dependencies {
    def modernUiCoreDep = 'icyllis.modernui:ModernUI-Core:3.12.0'
    def modernUiMcDep = "icyllis.modernui:ModernUI-NeoForge:${minecraft_version}-3.12.0.2@jar"

    api group: 'com.squareup.okhttp3', name: 'okhttp', version: okhttp_version
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.10.1'
    compileOnly(modernUiCoreDep) {
        transitive = false
    }
    compileOnly(modernUiMcDep) {
        transitive = false
    }

    compileOnly group: 'org.spongepowered', name: 'mixin', version: '0.8.5'
    // fabric and neoforge both bundle mixinextras, so it is safe to use it in common
    compileOnly group: 'io.github.llamalad7', name: 'mixinextras-common', version: '0.3.5'
    annotationProcessor group: 'io.github.llamalad7', name: 'mixinextras-common', version: '0.3.5'

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.11.4'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.11.4'
    testRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-launcher', version: '1.11.4'
}

configurations {
    commonJava {
        canBeResolved = false
        canBeConsumed = true
    }
    commonResources {
        canBeResolved = false
        canBeConsumed = true
    }
}

artifacts {
    commonJava sourceSets.main.java.sourceDirectories.singleFile
    commonResources sourceSets.main.resources.sourceDirectories.singleFile
}

tasks.named('test', Test) {
    useJUnitPlatform()
}

