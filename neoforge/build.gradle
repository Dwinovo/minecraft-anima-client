plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev'
}

dependencies {
    def okhttpDep = "com.squareup.okhttp3:okhttp:${okhttp_version}"
    def modernUiCoreDep = 'icyllis.modernui:ModernUI-Core:3.12.0'
    def modernUiMarkflowDep = 'icyllis.modernui:ModernUI-Markflow:3.12.0'
    def modernUiNeoForgeDep = "icyllis.modernui:ModernUI-NeoForge:${minecraft_version}-3.12.0.2@jar"

    jarJar(implementation(okhttpDep))
    compileOnly(modernUiCoreDep) {
        transitive = false
    }
    compileOnly(modernUiMarkflowDep) {
        transitive = false
    }
    implementation modernUiNeoForgeDep
}

afterEvaluate {
    def okhttpDep = "com.squareup.okhttp3:okhttp:${okhttp_version}"
    def modernUiCoreDep = 'icyllis.modernui:ModernUI-Core:3.12.0'
    def modernUiMarkflowDep = 'icyllis.modernui:ModernUI-Markflow:3.12.0'
    def modernUiNeoForgeDep = "icyllis.modernui:ModernUI-NeoForge:${minecraft_version}-3.12.0.2@jar"
    def modernUiExclude = {
        exclude group: 'org.slf4j', module: 'slf4j-api'
        exclude group: 'org.apache.logging.log4j', module: 'log4j-core'
        exclude group: 'org.apache.logging.log4j', module: 'log4j-api'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
        exclude group: 'org.jetbrains', module: 'annotations'
        exclude group: 'com.ibm.icu', module: 'icu4j'
        exclude group: 'it.unimi.dsi', module: 'fastutil'
    }
    dependencies {
        add("clientAdditionalRuntimeClasspath", okhttpDep)
        add("serverAdditionalRuntimeClasspath", okhttpDep)
        add("dataAdditionalRuntimeClasspath", okhttpDep)
        add("clientAdditionalRuntimeClasspath", dependencies.create(modernUiCoreDep, modernUiExclude))
        add("clientAdditionalRuntimeClasspath", dependencies.create(modernUiMarkflowDep, modernUiExclude))
    }
}

neoForge {
    version = neoforge_version
    // Automatically enable neoforge AccessTransformers if the file exists
    def at = project(':common').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from(at.absolutePath)
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
    runs {
        configureEach {
            systemProperty('neoforge.enabledGameTestNamespaces', mod_id)
            ideName = "NeoForge ${it.name.capitalize()} (${project.path})" // Unify the run config names with fabric
        }
        client {
            client()
        }
        data {
            data()

            // DataGen can be run by - "./gradlew :neoforge:runData" in Terminal.
            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            programArguments.addAll '--mod', project.mod_id, '--all',
                    '--output', file('src/generated/resources/').getAbsolutePath(),
                    '--existing', rootProject.file('common/src/main/resources').getAbsolutePath()

        }
        server {
            server()
        }
    }
    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }
